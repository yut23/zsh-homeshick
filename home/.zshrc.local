# ~/.zshrc.local
# Note: this file is sourced from a function in ~/.zshrc (zrclocal), so any
# local variables are effectively scoped to this file.

# set HOSTNAME if not already set
: ${HOSTNAME:=$(hostname)}
# allow override through dotfile
if [[ -f "$HOME/.hostname" ]]; then
  HOSTNAME=$(<"$HOME/.hostname")
fi
export HOSTNAME

# Figure out what system we're on
if [[ -z "${system_name:+x}" ]]; then
  if [[ -n "${NERSC_HOST:+x}" ]]; then
    system_name=$NERSC_HOST
  elif [[ -n "${LMOD_SYSTEM_NAME:+x}" ]]; then
    system_name=$LMOD_SYSTEM_NAME
  elif [[ "$HOSTNAME" == dtn<->.ccs.ornl.gov ]]; then
    system_name=olcf-dtn
  elif [[ "$HOSTNAME" == home<->.ccs.ornl.gov ]]; then
    system_name=olcf-home
  else
    system_name=$HOSTNAME
  fi
fi

# work around some weird bug with OverlayFS encountered on cori08
if [[ $system_name == cori ]] && ! readlink /etc/zsh.zshrc.local &>/dev/null; then
  # /etc/zsh.zshrc.local should point to /etc/bash.bashrc.local
  source "/etc/bash.bashrc.local"
fi

source "$HOME/.zsh/paths.zsh"
if [[ -n "${system_name:+x}" && -f "$HOME/.zsh/$system_name/modules.zsh" ]]; then
  if [[ -z "${_MODULES_LOADED+x}" ]]; then
    # set this before sourcing, so the local modules.zsh can modify it if needed
    export _MODULES_LOADED=1
    source "$HOME/.zsh/$system_name/modules.zsh"
  fi
fi

# If in ssh, get into tmux ASAP
if [[ -f "$HOME/.tmux/autotmux.zsh" ]]; then
  source "$HOME/.tmux/autotmux.zsh"
fi

# Disable coredumps
ulimit -c 0

# Disable flow control keybindings (ctrl-s and ctrl-q)
stty -ixon

source "${HOME}/.zsh/zshrc"
