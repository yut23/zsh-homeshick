# ~/.zshrc.local
# Note: this file is sourced from a function in ~/.zshrc (zrclocal), so any
# local variables are effectively scoped to this file.

# set HOSTNAME if not already set
: ${HOSTNAME:=$(hostname)}
# allow override through dotfile
if [[ -f "$HOME/.hostname" ]]; then
  HOSTNAME=$(<"$HOME/.hostname")
fi
export HOSTNAME

ZSH_CONFIG="$HOME/.zsh"

# Figure out what system we're on
if [[ -z "${system_name:+x}" ]]; then
  source "$ZSH_CONFIG/system_name.zsh"
fi
export system_name

# work around some weird bug with OverlayFS encountered on cori08
if [[ $system_name == cori ]] && ! readlink /etc/zsh.zshrc.local &>/dev/null; then
  # /etc/zsh.zshrc.local should point to /etc/bash.bashrc.local
  source "/etc/bash.bashrc.local"
fi
if [[ $system_name == perlmutter ]] && ! (( $+commands[module] )); then
  source "/etc/profile.d/zz-cray-pe.sh"
fi

function source_config_files() {
  local file="$1"
  if [[ -f "$ZSH_CONFIG/$file" ]]; then
    source "$ZSH_CONFIG/$file"
  fi
  if [[ -n "${system_name:+x}" && -f "$ZSH_CONFIG/$system_name/$file" ]]; then
    source "$ZSH_CONFIG/$system_name/$file"
  fi
}

source_config_files "paths.zsh"

if [[ -n "${system_name:+x}" && -f "$ZSH_CONFIG/$system_name/modules.zsh" ]]; then
  if [[ -z "${_MODULES_LOADED+x}" ]]; then
    # set this before sourcing, so the local modules.zsh can modify it if needed
    export _MODULES_LOADED=1
    source "$ZSH_CONFIG/$system_name/modules.zsh"
  fi
fi

# If in ssh, get into tmux ASAP
if [[ -f "$HOME/.tmux/autotmux.zsh" && $HOST != ZB<->-host ]]; then
  source "$HOME/.tmux/autotmux.zsh"
fi

# Disable coredumps
#ulimit -c 0

# Disable flow control keybindings (ctrl-s and ctrl-q)
if [[ -t 0 ]]; then
  stty -ixon
fi

# Homeshick
source "$HOME/.homesick/repos/homeshick/homeshick.sh"

source_config_files "env.zsh"

source_config_files "aliases.zsh"
source_config_files "functions.zsh"
source_config_files "history.zsh"
source_config_files "misc.zsh"

# Zinit plugins
source_config_files "plugins.zsh"

source_config_files "keys.zsh"

source_config_files "private.zsh"

# clean up after ourselves
unfunction source_config_files
