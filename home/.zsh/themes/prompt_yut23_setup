# ZSH Theme - Preview: http://cl.ly/f701d00760f8059e06dc
# Thanks to gallifrey, upon whose theme this is based

function prompt_yut23_setup {
  local exit_code="%(?..%{$fg_bold[red]%}%?%{$reset_color%})"
  prompt_opts=(cr sp percent subst)

  local -a groups
  groups=($(id -G))
  local is_root=
  local prompt_symbol user_color
  # check if gid 0 is present in the groups array
  if (( ${+groups[(r)0]} )); then
    prompt_symbol="%{$fg_bold[red]%}#%{$reset_color%}"
    user_color="$fg_bold[red]"
  else
    prompt_symbol='\$'
    user_color="$fg[green]"
  fi
  if (( SHLVL > 1 )); then
    prompt_symbol="$SHLVL$prompt_symbol"
  fi
  # add magenta cube for toolbx(1) sessions
  if [ -f /run/.containerenv ] && [ -f /run/.toolboxenv ]; then
    prompt_symbol="%{$fg[magenta]%}⬢%{$reset_color%} $prompt_symbol"
  fi
  local user="%{$user_color%}%n%{$reset_color%}"

  # different color for ssh session
  if [[ -n "${SSH_CONNECTION+1}" ]]; then
    # ssh
    local host="%{$fg[cyan]%}@$HOSTNAME%{$reset_color%}"
  else
    # local
    local host="%{$fg[green]%}@$HOSTNAME%{$reset_color%}"
  fi
  local current_dir="%{$fg[yellow]%}%~%{$reset_color%}"
  local git_prompt='$(_my_git_prompt_info)'
  local throttled=''
  if [[ $system_name == summit && -e "/sys/fs/cgroup/cpu/user.slice/user-$UID.slice" ]]; then
    throttled='$(_check_throttled)'
  fi

  PS1="
${user}${host}${throttled} ${current_dir} ${git_prompt}
${prompt_symbol} "
  RPS1="%{$fg[green]%}\$(_virtualenv_info)%{$reset_color%}${exit_code}"

  zle -N zle-keymap-select
  zle -N zle-line-finish
}

function _virtualenv_info {
  if [[ -n "$CONDA_DEFAULT_ENV" ]]; then
    echo "($CONDA_DEFAULT_ENV) "
  fi
  if [[ -n "$VIRTUAL_ENV" ]] then
    echo "%{$fg[blue]%}(${VIRTUAL_ENV:t}) "
  fi
}

function _check_throttled {
  local quota=$(<"/sys/fs/cgroup/cpu/user.slice/user-$UID.slice/cpu.cfs_quota_us")
  local period=$(<"/sys/fs/cgroup/cpu/user.slice/user-$UID.slice/cpu.cfs_period_us")
  if (( quota / period != 16 )); then
    echo " %{$fg_bold[red]%}throttled%{$reset_color%}"
  fi
}

function zle-keymap-select {
  zle reset-prompt
  case $TERM in
    linux|dumb) ;;
    *) [[ $KEYMAP == 'vicmd' ]] && printf '\033[1 q' || printf '\033[5 q' ;;
  esac
}

function zle-line-finish {
  case $TERM in
    linux|dumb) ;;
    *)
      printf '\033[5 q'
      # from GRML zshrc
      zle-rmkx
      ;;
  esac
}

function _my_git_prompt_info() {
  # largely based on Prezto's git-info function
  local branch git_status format info
  local branch_oid branch_head
  local git_cmd status_cmd
  local ahead=0
  local behind=0
  local gone=0
  local detached=0
  local has_upstream=0
  local upstream_exists=0

  local added=0
  local deleted=0
  local modified=0
  local renamed=0
  local unmerged=0
  local untracked=0
  local dirty=0

  # normal array
  local -a format_keys
  format_keys=(
    behind
    ahead
    gone
    unmerged
    deleted
    renamed
    modified
    added
    untracked
  )
  # associative array (unordered)
  local -A symbols
  symbols=(
    behind '↓'
    ahead '↑'
    gone '✗'
    unmerged '?'
    deleted '!'
    renamed '~'
    modified '*'
    added '+'
    untracked '%%'
  )
  [[ $#symbols == $#format_keys ]] || print -ru2 "mismatch between symbols and format_keys"

  # Use porcelain status for easy parsing.
  git_cmd='command git'
  if (( $+commands[etckeeper] )); then
    case $PWD/ in
      /etc/*)
        git_cmd='command sudo -p"[etckeeper] password for %u: " etckeeper vcs' ;;
    esac
  fi
  status_cmd="$git_cmd status --porcelain=v2 --branch"

  # Get current status, branch name, and commit differences with a single git call.
  while IFS=$'\n' read line; do
    if [[ "$line" == \#\ * ]]; then
      if [[ "$line" == '# branch.oid '* ]]; then
        branch_oid=${line#'# branch.oid '}
      elif [[ "$line" == '# branch.head '* ]]; then
        branch_head=${line#'# branch.head '}
      elif [[ "$line" == '# branch.upstream '* ]]; then
        has_upstream=1
      elif [[ "$line" =~ '^# branch.ab \+([0-9]+) -([0-9]+)$' ]]; then
        upstream_exists=1
        ahead=${match[1]}
        behind=${match[2]}
      fi
      # don't increment $dirty
      continue
    fi
    # Count added, deleted, modified, renamed, unmerged, untracked, dirty.
    # T (type change) is undocumented, see https://git.io/FnpMGw.
    # For a table of scenarii, see https://i.imgur.com/2YLu1.png.
    [[ "$line" == ?\ ([ACDMT][.MT]|[ACMT]D)\ * ]] && (( added++ ))
    [[ "$line" == ?\ [.ACMRT]D\ * ]] && (( deleted++ ))
    [[ "$line" == ?\ ?[MT]\ * ]] && (( modified++ ))
    [[ "$line" == 2\ R?\ * ]] && (( renamed++ ))
    [[ "$line" == u\ * ]] && (( unmerged++ ))
    [[ "$line" == \?\ * ]] && (( untracked++ ))
    (( dirty++ ))
  done < <(${(Q)${(z)status_cmd}} 2> /dev/null)

  if [[ $branch_head == '(detached)' ]]; then
    branch="${branch_oid:0:7}"
    detached=1
  else
    branch="$branch_head"
  fi

  if (( has_upstream == 1 && upstream_exists == 0 )); then
    gone=1
  fi

  [[ -z $branch ]] && return

  local -A formats
  formats=(
    behind B
    ahead A
    gone G
    unmerged U
    deleted d
    renamed r
    modified m
    added a
    untracked u
  )
  [[ $#formats == $#format_keys ]] || print -ru2 "mismatch between formats and format_keys"

  local -a replacements
  for key in ${format_keys[@]}; do
    # check values against 0, and replace with nothing if equal
    # need extra escaping for zformat
    format="$format%0($formats[$key]..${symbols[$key]//\%/%%})"
    replacements+=("$formats[$key]:${(P)key}")
  done
  zformat -f git_status "$format" "${replacements[@]}"

  # strip trailing space in case $git_status is empty
  if (( dirty > 0 )); then
    color=red
  elif [[ -n $git_status ]]; then
    color=yellow
  else
    color=green
  fi
  if (( detached )); then
    # highlight detached branch in cyan
    branch="%{$fg[cyan]%}:$branch%{$fg[$color]%}"
  fi
  info="$branch $git_status"
  # strip trailing space if git_status is empty
  info="${info% }"
  echo "%{$terminfo[bold]$fg[$color]%}($info)%{$reset_color%}"
}

prompt_yut23_setup
# vim: filetype=zsh
