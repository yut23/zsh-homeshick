# ZSH Theme - Preview: http://cl.ly/f701d00760f8059e06dc
# Thanks to gallifrey, upon whose theme this is based

#ZSH_THEME_GIT_PROMPT_PREFIX="${GIT_COLOR}("
ZSH_THEME_GIT_PROMPT_SUFFIX=")%{$reset_color%}"
ZSH_THEME_GIT_PROMPT_UNTRACKED="%%"
ZSH_THEME_GIT_PROMPT_ADDED="+"
ZSH_THEME_GIT_PROMPT_MODIFIED="*"
ZSH_THEME_GIT_PROMPT_RENAMED="~"
ZSH_THEME_GIT_PROMPT_DELETED="!"
ZSH_THEME_GIT_PROMPT_UNMERGED="?"
ZSH_THEME_GIT_PROMPT_AHEAD="â†‘"
ZSH_THEME_GIT_PROMPT_BEHIND="â†“"
ZSH_THEME_GIT_PROMPT_DIVERGED="â†•"

function prompt_yut23_setup {
  local exit_code="%(?..%{$fg_bold[red]%}%?%{$reset_color%})"
  prompt_opts=(cr sp percent subst)

  # different color for ssh session
  local user='%{$fg[green]%}%n%{$reset_color%}'
  if [[ -n "${SSH_CLIENT+1}" ]]; then
    # ssh
    local host='%{$fg[cyan]%}@'$HOSTNAME'%{$reset_color%}'
  else
    # local
    local host='%{$fg[green]%}@'$HOSTNAME'%{$reset_color%}'
  fi
  local current_dir='%{$fg[yellow]%}%~%{$reset_color%}'
  local git_prompt='$(my_git_prompt_info)'
  local groups
  groups=($(id -G))
  # magic subscript globbing ahead
  (( ${+groups[(r)0]} )) && prompt_symbol='#' || prompt_symbol='$'

  PS1="
${user}${host} ${current_dir} ${git_prompt}
${prompt_symbol} "
  RPS1="%{$fg[green]%}\$(virtualenv_info)%{$reset_color%}${exit_code}"

  zle -N zle-keymap-select
  zle -N zle-line-finish
}

function virtualenv_info {
  [[ -n "$VIRTUAL_ENV" ]] && echo '('${VIRTUAL_ENV:t}') '
}

function zle-keymap-select {
  zle reset-prompt
  [[ ($TERM =~ "xterm.*" || $TERM =~ "screen.*") && $KEYMAP == 'vicmd' ]] && echo -n '[1 q' || echo -n '[5 q'
}

function zle-line-finish {
  [[ $TERM =~ "xterm.*" || $TERM =~ "screen.*" ]] && echo -n '[5 q'
}

function my_git_prompt_info() {
  local ref
  ref=$(git symbolic-ref HEAD 2> /dev/null) || return
  GIT_STATUS=$(git_prompt_status)
  if [[ -n $GIT_STATUS ]]; then
    if [[ $GIT_STATUS =~ [%+*~\!?] ]]; then
      echo "%{$terminfo[bold]$fg[red]%}(${ref#refs/heads/} $GIT_STATUS$ZSH_THEME_GIT_PROMPT_SUFFIX"
    else
      echo "%{$terminfo[bold]$fg[yellow]%}(${ref#refs/heads/} $GIT_STATUS$ZSH_THEME_GIT_PROMPT_SUFFIX"
    fi
  else
    echo "%{$terminfo[bold]$fg[green]%}(${ref#refs/heads/}$ZSH_THEME_GIT_PROMPT_SUFFIX"
  fi
}

# vim: filetype=zsh
